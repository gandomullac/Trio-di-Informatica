	.data
msg1:	.asciiz	"Il numero "		# Testo in output 1
msg2:	.asciiz	" è stato estratto "	# Testo in output 2
msg3:	.asciiz " volte."		# Testo in output 3

N:	.word 40			# Valore di N	(numero iterazioni)
M:	.word 3				# Valore di M	(numero ricercato)

	.text				# Seguono le linee di codice
MAIN: 
	lw $s0, N			# Carico la word contenente N
	lw $s1, M			# Carico la word contenente M
	
	add $a1, $s0, $zero		# Copio il valore di N nel registro degli argomenti
	jal D6				# Chiamata a funzione
	
	add $a1, $s0, $zero		# Copio il valore di N nel registro degli argomenti
	add $a2, $s1, $zero		# Copio il valore di M nel registro degli argomenti	
	jal READ_STACK			# Chiamata a funzione
	
	add $s2, $v0, $zero		# Salvo numero delle frequenze di M
	add $a3, $s2, $zero		# Copio il numero delle frequenze di M in un registro degli argomenti

	jal PRINT			# Chiamata a funzione
	
	li $v0, 10			# Predispongo la variabile della chiamata a sistema a 10 -> Termina esecuzione
	syscall				# Chiamata a sistema


D6:
	add $t2, $ra, $zero		# Salvo l'indirizzo della funzione chiamante in una variabile temporanea
	move $t1, $a1			# Imposto il numero di iterazioni
	li $t0, 0			# Imposto l'indice
	li $a1 6			# Impongo il limite alto per la generazione del numero casuale 
D6_loop:	
	li $v0 42			# Predispongo la variabile della chiamata a sistema a 42 -> random int range
	li $a0 222			# ID di generazione del numero casuale
	syscall				# Ottengo il numero causale in $a0
	
	addi $a0, $a0, 1		# Aggiungo 1 al valore, passando da un intervallo [0;5] ad un intervallo [1;6].
					# Rimango in $a0 poiché è argomento della prossima funzione
	jal PUSH			# Chiamata di funzione
	
	addi $t0, $t0, 1		# Incremento l'indice
	bne $t0, $t1, D6_loop		# Se l'indice è diverso da N, ripeti il loop
	
	jr $t2				# Ritorno alla funzione chiamante
	
	
PUSH:	
	# Ottengo il valore in entrata e lo carico nello stack
	addi $sp,$sp,-4
	sw $a0,($sp)
	
	jr $ra
	
	
READ_STACK:
	# ritorna il numero di volte in cui M si presenta
	add $t5, $ra, $zero		# Salvo l'indirizzo della funzione chiamante in una variabile temporanea
	add $t1, $a1, $zero		# Imposto il numero di iterazioni
	li $t0, 0			# Imposto l'indice
	li $v0, 0			# Imposto il contatore
	add $t3, $a2, $zero		# Salvo temporaneamente il valore ricercato

READ_loop:	
	lw $t4, 0($sp) 			# Leggo dallo stack e scrivo in $t4
	addi $sp, $sp 4 		# Incremento $sp di 4
	
	addi $t0, $t0, 1		# Incremento l'indice
	beq $t3, $t4, counter		# Se il valore letto corrisponde a quello ricercato, incrementa il contatore
	bne $t0, $t1, READ_loop		# Se l'indice è diverso da N, ripeto il loop
	jr $t5				# Altrimenti ritorno alla funzione chiamante
counter:	
	addi $v0, $v0, 1		# Incremento il contatore				
	bne $t0, $t1, READ_loop		# Se l'indice è diverso da N, ripeto il loop
	jr $t5				# Altrimenti ritorno alla funzione chiamante


PRINT:
	la $a0, msg1			# Carico la parola
	li $v0, 4			# Predispongo la variabile della chiamata a sistema a 4 -> Stampa stringa
	syscall				# Chiamata a sistema
	
	add $a0, $a2, $zero		# Salvo M nel registro dell'argomento della syscall
	li $v0, 1			# Predispongo la variabile della chiamata a sistema a 1 -> Stampa intero
	syscall				# Chiamata a sistema
	
	la $a0, msg2			# Carico la parola
	li $v0, 4			# Predispongo la variabile della chiamata a sistema a 4 -> Stampa stringa
	syscall				# Chiamata a sistema
	
	add $a0, $a3, $zero		# Salvo il numero delle frequenze di M nel registro dell'argomento della syscall
	li $v0, 1			# Predispongo la variabile della chiamata a sistema a 1 -> Stampa intero
	syscall				# Chiamata a sistema
	
	la $a0, msg3			# Carico la parola
	li $v0, 4			# Predispongo la variabile della chiamata a sistema a 4 -> Stampa stringa
	syscall				# Chiamata a sistema
	
	jr $ra				# Ritorno alla funzione chiamante